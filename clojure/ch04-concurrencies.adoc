= Chapter 4. Concurrency and Parallelism
:sectnums:
:toc:
:source-language: clojure

== Shifting Computation Through Time and Space

=== deref

`clojure.lang.IDeref` 인터페이스를 구현한 모든 객체는 ``deref``를 이용해 값을 읽어내야
한다.

[listing]
----
IDeref 구현 객체들
|-- mutables
|   |-- delays
|   |-- futures
|   `-- promises
`-- reference types
    |-- atoms
    |-- refs
    |-- agents
    `-- vars
----

==== Delay, Future, Promise 비교 정리

[listing]
----
          실행 쓰레드     실행 시기(딱 한번만)    값 초기화 
-------------------------------------------------------------
delay     동일 쓰레드     deref 호출시           정의시
future    다른 쓰레드     즉시                   정의시
promise   future 이용     deliver 호출시         deliver 내에서          
----

* 위 세 가지 모두 1회만 실행되고 그 결과값을 캐쉬에 저장하고 있다가 deref할 때마다 그
  저장된 값을 돌려준다는 공통점이 있다.

[source]
.예제
....
(def d (delay (apply + (range 1e8))))

@d   ; 동일 쓰레드에서 
; => 4999999950000000

(def f (future @d))  ; 다른 쓰레드에서
@f
; => 4999999950000000


(def p (promise))

@(future (deliver p 100))
@p
; => 100   
....




==== Mutables vs. References

[listing]
----
non-blocking         blocking   non-blocking    
----------------------------------------------
delay                deref
future               deref
promise              deref
deliver

atom                             deref
ref                              deref
agent                            deref
var                              deref
----

* ``delay/future/promise``에 대한 ``deref``는 blocking이지만, reference
  types(``atom/ref/agent/var``)에 대한 ``deref``는 언제나 non-blocking이다.
+
[source]
....
(def d (delay (apply + (range 1e8))))

; blocking
@d   ; => 4999999950000000


(def a (atom 100))

(future (Thread/sleep 3000)
        (reset! a (apply + (range 1e8))))

; non-blocking
@a   ; => 100

; 3초 후
@a   ; => 4999999950000000
....



=== Delay

* delay는 오직 한 번만 실행되고, 실행된 결과값은 캐싱된다.
+
[source]
....
(def d (delay (println "Running...")   ; non-blocking
              :done!))

(deref d)   ; blocking
; >> Running...
; => :done!

@d   
; => :done!
....


* 일반 함수 실행과의 차이점
+
[source]
....
(def a-fn (fn []
            (println "Running...")
            :done!))

(a-fn)
; >> Running...
; => :done!

(a-fn)
; >> Running...
; => :done!
....


* 전형적인 사용 예
+
[source]
....
(defn get-document [id]
  {:url "http://www.mozilla.org/about/manifesto.en.html"
   :title "The Mozilla Manifesto"
   :mime "text/html"
   :content (delay (slurp "http://www.mozilla.org/about/manifesto.en.html"))})

(def d (get-document "some-id"))

d
; => {:url "http://www.mozilla.org/about/manifesto.en.html",
;     :title "The Mozilla Manifesto",
;     :mime "text/html",
;     :content #<Delay@2efb541d: :pending>}


(realized? (:content d))
; => false

@(:content d)    ; blocking
; => "<!DOCTYPE html><html>..."

(realized? (:content d))
; => true
....

* `realized?` 함수는 delay, future, promise, lazy-seq에 사용 가능하다.

 
=== Futures

* future를 호출한 쓰레드에서, future는 곧바로 반환된다 (non-blocking).
* future 역시 delay와 마찬가지로 단 1번만 실행되고, 그 결과값은 캐시에 저장된다.
+
[source]
....
(def long-calculation (future (apply + (range 1e8))))   ; non-blocking

@long-calculation   ; => 4999999950000000
                    ; blocking

@long-calculation   ; => 4999999950000000
....

* deref에서 timeout 시간을 지정할 수도 있다.
+
[source]
....
(deref (future (Thread/sleep 5000) :done!)
       1000
       :impatient!)
; => :impatient!
....

* future에 주어진 코드는 다른 쓰레드에서 *즉시* 실행되기 시작한다.  
+
[source]
....
(defn get-document [id]
  {:url "http://www.mozilla.org/about/manifesto.en.html"
   :title "The Mozilla Manifesto"
   :mime "text/html"
   :content (future (slurp "http://www.mozilla.org/about/manifesto.en.html"))})

(def d (get-document "some-id"))

; 어느 정도 시간이 흐른 후 호출하면, non-blocking의 효과를 거둘 수 있다. 
@(:content d)
; => "<!DOCTYPE html><html>..."
....

* future는 Clojure가 관리하는 thread pool에서 동작한다. 이 thread pool은 agent도 공유해서
  이용한다.


=== Promises

* one-time, single-value pipe

* furure와 delay는 실행할 코드가 future와 delay 함수 내부에 담겨 있다.

* promise 자체에는 실행할 코드가 담겨 있지 않고, deliver 함수 내부에 담겨있다.

* promise를 deref하면, deliver 함수가 promise에 전달(deliver)한 값을 반환한다.

[source]
....
(def a (promise))   ; non-blocking
(def b (promise))
(def c (promise))

(future
  (deliver c (+ @a @b))   ; blocking
  (println "Delivery complete!"))

(deliver a 15)   
(deliver b 16)

;; future 종료 
; >> Delivery complete!

@c   ; => 31
....


== Parallelism on the Cheap

=== Concurrency vs. Parallelism

* Concurrency(동시성 처리): 여러 개의 쓰레드를 이용한 순차처리(쓰레드 간의 실행순서
  조절(동기화) 필요)

* Parallelism(병렬 처리): 여러 개의 쓰레드를 이용한 동시처리(쓰레드 간의 실행순서
  조절(동기화) 불필요)

[listing]
----
Concurrency: a --> b --> c
Parallelism: a + b + c 
----

[listing]
.클로저의 Parallelsim 처리 방식
----
|-- agents
|-- pmap/pcalls/pvalues
`-- clojure.core.reducers/fold (persistent vectors and maps)
----


=== `pamp`

* `pmap` 함수를 이용하면 비교적 손쉽게 parallelism을 구현할 수 있다.

* `pmap` 함수는 내부적으로 future를 호출한다.

* `pcalls` 함수는 내부적으로 `pamp` 함수를 호출한다.

* `pvalues` 함수는 내부적으로 `pcalls` 함수를 호출한다.

[source]
.사용 예
....
(defn phone-numbers [string]
  (re-seq #"(\d{3})[\.-]?(\d{3})[\.-]?(\d{4})" string))

(phone-numbers " Sunil: 617.555.2937, Betty: 508.555.2218")
; => (["617.555.2937" "617" "555" "2937"] ["508.555.2218" "508" "555" "2218"])

(def files (repeat 100
                   (apply str
                          (concat (repeat 1000000 \space)
                                  "Sunil: 617.555.2937, Betty: 508.555.2218"))))

(time (dorun (map phone-numbers files)))
; >> "Elapsed time: 2181.310679 msecs"

(time (dorun (pmap phone-numbers files)))
; >> "Elapsed time: 1088.884856 msecs"

;; dorun을 사용하지 않을 때
(time (map phone-numbers files))
; >> "Elapsed time: 0.09408 msecs"
....


[source]
.잘못 사용하는 예
....
(def files (repeat 100000
                   (apply str
                          (concat (repeat 1000 \space)
                                  "Sunil: 617.555.2937, Betty: 508.555.2218"))))

(time (dorun (map phone-numbers files)))
; >> "Elapsed time: 2649.807 msecs"

(time (dorun (pmap phone-numbers files)))
; >> "Elapsed time: 2772.794 msecs"
....



== Clojure Reference Types

=== References 비교

* 교재 p. 173 그림 참고
+
[listing]
----
refs     Global       synchronous  coordinated   changes
atoms    Global       synchrnous   uncoordinated changes
agents   Global       asynchronous uncoordinated changes
vars     Thread-local                            changes
----



[listing]
----
           생성                  읽기                 쓰기
---------------------------------------------------------------------------------
atom    (def b (atom 100))    (deref b)        (reset! b 200)
                                               (swap! b + 100)
ref     (def a (ref 100))     (deref a)        (dosync (ref-set a 200))
                                               (dosync (alter a + 10))
                                               (dosync (commute a + 10))
agent   (def c (agent 100))   (deref c)        (send c + 10)      ; CPU-intensive
                                               (send-off c + 10)  ; IO
----

=== identity and state

[listing]
----
Identities
|-- vars
|-- atoms
|-- refs
`-- agents
----

[source]
....
(def a (atom [1 2 3]))
@a        ; => [1 2 3]
a         ; => #<Atom@11fc720: [1 2 3]>

(reset! a [4 5 6])
@a        ; => [4 5 6]
a         ; => #<Atom@11fc720: [4 5 6]>
....

위의 코드에서 ``Atom@11fc720....``이 identity이고, 이 identity가 가리키고 있는 불변값 ``[1
2 3]``과 ``[4 5 6]`` 각각이 state에 해당한다.

* immutables의 경우 
+
[listing]
----
(def a [1 2 3])

symbol     var          immutable value
           identity     
-------------------------------------------------------------------------
a   --->   #'a   --->   [1 2 3]
----

* mutables의 경우
+
[listing]
----
(def a (atom [1 2 3]))
@a   ; => [1 2 3]

(reset! a [4 5 6])
@a   ; => [4 5 6]

symbol     var          atom                  immutable
           identity     identity              value
-------------------------------------------------------------------------
a   --->   #'a   --->   Atom@11fc720   --->   [1 2 3]
                                                 @a      (one state)
                                       --->   [4 5 6]
                                                 @a      (another state)
----

위에서 `[1 2 3]`, `[4 5 6]` 값(value) 자체는 immutable value이다. ``@a``는 이 서로 다른
객체에 대한 snapshot의 역할을 한다.


=== Helper macros

[source]
....
(defmacro futures [n & exprs]
  (vec (for [_ (range n)
             expr exprs]
         `(future ~expr))))

(defmacro wait-futures [& args]
  `(doseq [f# (futures ~@args)]
     @f#))
....

[source]
....
(for [_    (range 2)
      expr [:a :b :c]]
  expr)
; => (:a :b :c :a :b :c)
....




=== Atoms

==== reset!

* `(reset! atom new-value)`

[source]
....
(def a (atom 10))

@a   ; => 10

(reset! a 20)   ; => 20

@a   ; => 20
....


==== swap!

* `(swap! atom f arg*)`
+
이때 함수 ``f``는 다음과 같이 실행된다: `(f @atom arg*)`
+
`(swap! atom f arg*)` == `(reset! atom (f @atom args*))`
+
[source]
....
(def xs (atom #{1 2 3}))

(wait-futures 1 (swap! xs (fn [v]
                              (Thread/sleep 250)
                              (println "trying 4")
                              (conj v 4)))
                (swap! xs (fn [v]
                            (Thread/sleep 500)
                            (println "trying 5")
                            (conj v 5))))
; >> trying 4
; >> trying 5
; >> trying 5

@xs
; => #{1 2 3 4 5}
....

* p. 175 그림 참고



==== compare-and-set!

* `(comapre-and-set! atom old-value new-value)`
+
[source]
....
(def a (atom #{1 2 3}))

@a   ; => #{1 2 3}

(compare-and-set! a 10 "new value")
; => false

@a   ; => #{1 2 3}

(compare-and-set! a @a "new value")
; => true

@a   ; => "new value"
....

* 주의: `compare-and-set!` 함수는 vaule를 비교하는 것이 아니라 reference(= pointer
  address)를 비교한다.
+  
[source]
....
(def xs (atom #{1 2}))

(compare-and-set! xs #{1 2} "new value")
; => false
....


==== Watches

* references(atom, ref, agent, var)의 값이 변경된 이후에 통지된다.

* watch는 함수이다. 일종의 callback function이다.

* 하나의 reference에 여러 개의 watch를 추가하거나 제거할 수 있다.

* `(watch key reference old-value new-value)`



[source]
....
(defn echo-watch [key atom old new]
  (println key old "=>" new))

(def sarah (atom {:name "Sarah" :age 25}))

(add-watch sarah :echo echo-watch)

; 동일한 값으로 변경할 때도 watch가 실행된다.
(reset! sarah {:name "Sarah" :age 25})
; >> :echo {:age 25, :name Sarah} => {:age 25, :name Sarah}
; => {:age 25, :name "Sarah"}

(swap! sarah update-in [:age] inc)
; >> :echo {:name Sarah, :age 25} => {:name Sarah, :age 26}
; => {:name "Sarah", :age 26}

(add-watch sarah :echo2 echo-watch)
                   
(swap! sarah update-in [:age] inc)                       
; >> :echo {:name Sarah, :age 26} => {:name Sarah, :age 27}
; >> :echo2 {:name Sarah, :age 26} => {:name Sarah, :age 27}
; => {:name "Sarah", :age 27}

(remove-watch sarah :echo2)

(swap! sarah update-in [:age] inc)
; >> :echo {:name Sarah, :age 27} => {:name Sarah, :age 28}
; => {:name "Sarah", :age 28}
....


==== Validators

* reference들의 값이 변경되기 전에, 변경 조건에 부합하는지를 먼저 검사한 후, 변경 조건을
  통과할 때에만 값을 변경하도록 해준다.

* validator는 인수를 한 개 받는 함수이어야 한다.

* vallidator가 logically false인 값들(false 또는 nil)을 리턴하거나 exception을 던지면,
  exception이 발생하면서 값의 변경이 취소된다.


[source]
....
(def n (atom 1 :validator pos?))

(swap! n + 500)
; => 501

(swap! n - 1000)
; => #<IllegalStateException java.lang.IllegalStateException: Invalid reference state>
....

[source]
....
(def sarah (atom {:name "Sarah" :age 25}))

(set-validator! sarah :age)

(swap! sarah dissoc :age)
; => #<IllegalStateException java.lang.IllegalStateException: Invalid reference state>
....


[listing]
.validator 설정 방식
----
         생성시(:validator)      생성후(set-validator!)
--------------------------------------------------------
atoms           O                         O
refs            O                         O
agents          O                         O
vars            X                         O
----

=== Refs

[sidebar]
.멀티 쓰레딩 프로그래밍의 어려움
****
* 실행 순서가 비결정적(nondeterministic)이어서 실행 흐름을 추적하기가 어렵다.

* 디버깅하기가 어렵다.

* test 버전에서는 제대로 동작하다가, release 버전에서는 에러를 낸다.

* 이 컴퓨터에서는 문제 없이 동작하다가, 저 컴퓨터에서는 에러를 낸다.

* 디버깅 모드에서는 제대로 동작하다가, 실행 모드에서는 에러를 낸다.

* 그런데 정작 우리나라의 프로그래머들은 사용하고 있는 framework이 멀티 쓰레딩을 이미
  알아서 처리해 주기 떄문에. 이런 멀티 쓰레딩 프로그램에 대한 어려움을 대부분 실감하지
  못하고 있다.

* 하지만 게임 서버를 직접 짜야한다거나 할 때에는 멀티 쓰레딩 프로그래밍을 피할 수 없다.
****

[sidebar]
.클로저에서의 멀티 쓰레딩 프로그래밍
****
클로저는 이런 문제에 대한 해결책을 라이브러리 차원이 아니라, 언어 차원에서 제공한다. 

* 기존의 언어들은 low-level의 thread와 lock으로 멀티 쓰레딩에 대처: race condition과
  deadlock 발생 가능성 상존

* 클로저는 high-level의 STM(Software Transaction Memory) 기법을 통해 문제 해결: livelock
  발생의 위험성은 있음

** 클로저의 자료형은 대부분 immutalbe형이라 thread-safe하다.

** 변화하는 상태를 관리할 필요가 있을 때에는 reference type을 사용한다.

** 위의 두 방법으로도 해결이 안될 때에는 Java의 Concurrency 라이브러리를 직접 사용한다.
****


==== `ref-set`

* atom의 `reset!` 함수처럼 초기화를 수행하는데 주로 쓰인다.

[source]
....
(dosync (ref-set bilbo {:name "Bilbo"}))
....


==== `alter`

[source]
....
(defn character [name & {:as opts}]
  (ref (merge {:name name :items #{} :health 500}
              opts)))

(def smaug (character "Smaug" :health 500 :strength 400 :items (set (range 50))))
(def bilbo (character "Bilbo" :health 100 :strength 100))
(def gandalf (character "Gandalf" :health 75 :mana 750))

(defn loot [from to]
  (dosync
    (when-let [item (first (:items @from))]
      (alter to update-in [:items] conj item)
      (alter from update-in [:items] disj item))))
  
(wait-futures 1
              (while (loot smaug bilbo))
              (while (loot smaug gandalf)))

(map (comp count :items deref) [bilbo gandalf])
; => (22 28)
....

* 참고: 교재 p. 184


==== `commute`

[source]
....
(def smaug (character "Smaug" :health 500 :strength 400 :items (set (range 50))))
(def bilbo (character "Bilbo" :health 100 :strength 100))
(def gandalf (character "Gandalf" :health 75 :mana 750))

(defn flawed-loot [from to]
  (dosync
   (when-let [item (first (:items @from))]
     (commute to update-in [:items] conj item)
     (commute from update-in [:items] disj item))))

(wait-futures 1
              (while (flawed-loot smaug bilbo))
              (while (flawed-loot smaug gandalf)))
 
(map (comp count :items deref) [bilbo gandalf])
; => (49 45)
....


[source]
....
(defn fixed-loot [from to]
  (dosync
   (when-let [item (first (:items @from))]
     (commute to update-in [:items] conj item)
     (alter from update-in [:items] disj item))))

(def smaug (character "Smaug" :health 500 :strength 400 :items (set (range 50))))
(def bilbo (character "Bilbo" :health 100 :strength 100))
(def gandalf (character "Gandalf" :health 75 :mana 750))

(time (wait-futures 1
                    (while (fixed-loot smaug bilbo))
                    (while (fixed-loot smaug gandalf))))
;= nil
(map (comp count :items deref) [bilbo gandalf])
; => (10 40)
....



==== The Sharp Corners of Software Transactional Memory

* transaction에서 참조하는 ref의 수를 최소화하라.

* STM 내에서 side-effect 함수를 사용해서는 안된다.
** 특히 IO 관련 함수는 사용해서는 안된다.
+
입출력 코드를 `io!` 매크로로 감싸면, `dosync` 함수에서 이 코드를 호출시 예외를 발생시켜 IO
함수 사용을 경고해 준다.
+ 
[source]
....
(defn unsafe []
  (io! (println "writing to database...")))

(dosync (unsafe))
; => #<IllegalStateException java.lang.IllegalStateException: I/O in transaction>
....

* STM 내의 값은 immutable해야 한다.

* 수행 시간이 긴 transaction은 짧은 transaction보다 더 많은 재시도가 일어나므로, 가능한
  수행 시간을 짧게 만들어야 한다.


===== Live lock: 수행 시간이 긴 transaction이 계속 retry할 때 발생한다.

클로저에서는 이런 현상을 막기 위한 대비책으로, 일정 시간 이상 계속 retry하는 transacion을
우선적으로 먼저 처리하는 barging을 시행한다. 

[source]
....
(def x (ref 0))

(dosync
  @(future (dosync (ref-set x 0)))
  (ref-set x 1))
; => #<RuntimeException java.lang.RuntimeException:
;      Transaction failed after reaching retry limit>

@x   ; => 0
....

===== Readers may retry

===== Write skew

* 참고: 교재 p. 197


=== Vars

==== Vars Are Not Variables

[source]
....
(def x 80)

(defn never-do-this []
  (def x 123)
  (def y 456)
  (def x (+ x y))
  x)

(never-do-this)   ; => 579
x                 ; => 579
....


==== vars의 용도

* 주로 개발 도구를 구현할 때 이용된다.

* 다른 언어에서의 전역 상수(?)의 역할을 수행한다.

* var의 값을 변경할 수는 있지만, 주로 개발시에만 이용해야 한다.


[source]
....
(def a 10)
(def b a)
(def c #'a)

; symbol      var       value
; ------------------------------
; a  --->    #'a  --->   10
;                        ^
;                        |
; b  --->    #'b  --------


; a  --->    #'a  --->   10
;             ^   
;             |
; c  --->    #'c


(def a 20)

b   ; => 10
c   ; => #'user/a
@c  ; => 20


; symbol      var       value
; ------------------------------
; a  --->    #'a  --->   20
;             ^   
;             |
; c  --->    #'c
....


==== Private vars

[source]
....
user> (def ^:private everything 42)

; 위와 동일
; (def ^{:private true} everything 42)

; 다른 namespace로 이동
user> (ns other-namespace)

; user namespace를 refer하더라도 public var들만 참조 가능
other-namespace> (refer 'user)

other-namespace> everything
; >> #<CompilerException java.lang.RuntimeException:
;    Unable to resolve symbol: everything in this context, compiling:(NO_SOURCE_PATH:0)>

; var를 통해서는 접근 가능
other-namespace> @#'user/everything
; => 42
....

[source]
.private function
....
user> (defn- my-add [a b]
        (+ a b))

user> (ns other-namespace)

other-namespace> (refer 'user)

; private function 테스트할 때 유용
other-namespace> (#'user/my-add 10 20) 
30
....


[source]
.with-redefs
....
(ns http)

(defn post [url]
  {:body "Hello world"})


;; 다른 namespace에서
(ns app
  (:require [clojure.test :refer [deftest is run-tests]]))

(deftest is-a-macro
  (with-redefs [http/post (fn [url] {:body "Goodbye world"})]
    (is (= {:body "Goodbye world"} (http/post "http://service.com/greet")))))

(run-tests) ;; test is passing
....


==== Var and metadata

* var에 metadata가 붙어 있어서, metadata를 읽으려먼 var를 통해야 한다.

[source]
....
(def a
  "A sample value."
 5)

(meta #'a)
; => {:ns #<Namespace user>, :name a, :doc "A sample value.",
;     :line 1, :file "NO_SOURCE_PATH"}

(doc a)
; -------------------------
; user/a
; A sample value.

(alter-meta! #'a assoc :doc "A dummy value.")

(doc a)
; -------------------------
; user/a
; A dummy value.
....

==== Constant

[source]
....
(def max-value 250)

(defn valid-value? [v]
  (<= v max-value))

(valid-value? 218)   ; => true
(valid-value? 299)   ; => false
....

[source]
....
(def ^:const max-value 250)

(defn valid-value? [v]
  (<= v max-value))

; 위의 코드는 다음의 코드와 같다.
; (defn valid-value? [v]
;   (<= v 250))

(def max-value 500)
(valid-value? 299)   ; => false
....


==== Dynamic binding: Thread-local changes

[source]
....
(def ^:dynamic *var* :root)

(defn get-*var* [] (println *var*))


(get-*var*)             ; >> :root

(binding [*var* :a]
  (get-*var*)           ; >> :a

  (binding [*var* :b]
    (get-*var*)         ; >> :b

    (binding [*var* :c]
      (get-*var*))      ; >> :c

    (get-*var*))        ; >> :b

  (get-*var*))          ; >> :a

(get-*var*)             ; >> :root
....


* Visualizing dynamic scope.: 교재 p.203

* Dynamic var의 경우는, 대개 상위 코드에서 ``binding``으로 설정한 값을 하위 코드에서 읽어
  들이는 방식으로 동작한다.

* 하지만 하위 코드에서 `set!` 함수를 통해 var를 설정할 수도 있다.

[source]
....
(def ^:dynamic *response-code* nil)

(defn http-get [url-string]
  (let [conn (-> url-string java.net.URL. .openConnection)
        response-code (.getResponseCode conn)]
    (when (thread-bound? #'*response-code*)
      (set! *response-code* response-code))
    (when (not= 404 response-code) (-> conn .getInputStream slurp))))

(http-get "http://google.com")
; => "<!doctype html><html><head>..."

*response-code*
; => nil

(binding [*response-code* nil]
  (let [content (http-get "http://google.com/bad-url")]
    (println "Response code was:" *response-code*)
    ; ... do something with `content` if it is not nil ...
    ))
; => Response code was: 404
....


==== Changing a var's root binding

* Aspect-oriented programming에 이용 가능

[source]
....
 (defn square [n] 
  "Squares a number"
  (* n n))

(defn wrap-fn [f]
  (fn [num]
    (println "square starts.")
    (let [result (f num)]
      (println "square ends.")
      result) ))

(alter-var-root #'square wrap-fn)

(square 10)
; >> square starts.
; >> square ends.
; => 100
....



==== Forward Declarations

[source]
....
(declare complex-helper-fn other-helper-fn)


(defn public-api-function [arg1 arg2]
  ,,,
  (other-helper-fn arg1 arg2 (complex-helper-fn arg1 arg2))


(defn- complex-helper-fn [arg1 arg2]
  ,,,)

(defn- other-helper-fn [arg1 arg2 arg3]
  ,,,)
....


=== Agents

==== 특징

* Uncoordinated: 다른 agent와 독립적으로 실행된다. 

* Asynchronous: ``send``와 ``send-off``를 이용해 agent를 호출하는 쓰레드와, agent를
                실행하는 쓰레드가 별개이다.
** agent를 실행하는 send의 경우
+
[source]
....
(def a (agent 10))

(send a inc)
....

** ref를 실행하는 ``dosync``의 경우는, ``dosync``를 호출하는 쓰레드와 ``dosync``를 실행하는 쓰레드가 동일하다. 
+
[source] 
....
(def names (ref []))

(defn add-name [name]
  (dosync
    (alter names conj name)))

(future (add-name "zack"))
....

* atom, ref와는 다르게 side-effect 함수를 안전하게 처리할 수 있다.
** STM-aware: STM 내에서 사용할 수 있다. 즉, `dosync` 내에서 호출되어 retry가 여러 번
   일어나더라도, dosync 내에서 호출되는 agent는 dosync가 commit될 때 단 한 번만
   호출된다. (교재 p.217 참고)

* 각각의 agent는 한 개의 queue를 갖고 있다. `send`/`send-off` 함수가 보내온 작업을 이
  queue에 쌓아두고 보내온 순서대로 한 개씩 처리한다.


==== `send` & `send-off`

* `send`
** 고정된 크기(CPU core 수 * 2)의 thread-pool에서 nonblocking, CPU-bound actions들을 처리.

* `send-off`
** 무제한의 크기를 가질 수 있는 thread-pool(future와 공유)에서 blocking, non-CPU-bound
   actions들을 처리.

* 교재 p. 210 그림 참조


==== 사용 예제

[source]
.에제 1
....
(def a (agent 500))

(send a range 1000)
@a   ; => (500 501 502 503 504 ... 999)
....


[source]
.에제 2
....
(def b (agent 10))

(send b inc)
(send b inc)

@b   ; => 12
....


[source]
.`await` 에제
....
(def a (agent 5000))
(def b (agent 10000))

(send-off a (fn [ms] (Thread/sleep ms) 10))
(send-off b (fn [ms] (Thread/sleep ms) 20))

@a   ; => 5000

(await a b)

@a   ; => 10
@b   ; => 20
....


==== Dealing with Errors in Agent Actions

[source]
....
(def a (agent 10))

(send a (fn [_] (throw (Exception. "something is wrong"))))

a    ; => #<Agent@3d582527 FAILED: 10>
@a   ; => 10

(send a inc)
; >> Caused by java.lang.Exception
;    something is wrong
....


[source]
....
(restart-agent a 42)

(send a inc)

(reduce send a (for [x (range 3)]
                 (fn [_] (throw (Exception. (str "error #" x))))))

(agent-error a)
; => #<Exception java.lang.Exception: error #0>

(restart-agent a 42)
(agent-error a)
; => #<Exception java.lang.Exception: error #1>

(restart-agent a 42 :clear-actions true)

(agent-error a)
; => nil

@a   ; => 42
....


===== Agent error handlers and modes

[listing]
.:error-mode
----
  :fail (default)
  :continue
----

[source]
....
(def a (agent 10 :error-mode :continue))

(send a (fn [_] (throw (Exception. "something is wrong"))))
; => #<Agent@44a5b703: 10>

(send a identity)
;= #<Agent@44a5b703: 10>
....


[source]
....
(def a (agent 20
              :error-mode :continue
              :error-handler (fn [the-agent exception]
                               (.println System/out (.getMessage exception)))))

(send a (fn [_] (throw (Exception. "something is wrong"))))
; >> something is wrong
; => #<Agent@bb07c59: 20>

(send a identity)
: => #<Agent@bb07c59: 20>
....


== Using Java's Concurrency Primitives

* Java interop을 통해서 `java.util.concurrent.*` 패키지들이 제공하는 동시성 처리
  클래스들을 직접 이용할 수도 있다.

=== Locking

[source]
....
(defn add [some-list value]
  (locking some-list
    (.add some-list value)))
....


[source]
....
// Java
public static void add (java.util.List someList, Object value) {
  synchronized (someList) {
    someList.add(value);
  }
}
....

